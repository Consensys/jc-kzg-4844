plugins {
    id "java-library"
    id "me.qoomon.git-versioning" version "6.4.3"
    id "maven-publish"
    id "org.jreleaser" version "1.16.0"
    id "signing"
}

repositories {
    mavenCentral()
}

group = "io.consensys.protocols"

version = "UNKNOWN"

gitVersioning.apply {
    refs {
        tag(".+") {
            version = "\${ref}"
        }
        branch("master") {
            version = "develop"
        }
        branch(".+") {
            version = "\${ref}"
        }
    }
    rev {
        version = "UNKNOWN"
    }
}

java {
    sourceCompatibility = 17
    targetCompatibility = 17
    withJavadocJar()
    withSourcesJar()
}

signing {
    useGpgCmd()
    sign publishing.publications
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name.set(project.name)
                description.set("Java wrapper around C-KZG-4844")
                url.set("http://github.com/Consensys/jc-kzg-4844")

                licenses {
                    license {
                        name.set("The Apache License, Version 2.0")
                        url.set("http://www.apache.org/licenses/LICENSE-2.0.txt")
                    }
                }

                developers {
                    developer {
                        id.set("consensys")
                        name.set("Protocols Team")
                        email.set("devops@consensys.net")
                    }
                }

                scm {
                    connection.set("scm:git:git://github.com/Consensys/jc-kzg-4844.git")
                    developerConnection.set("scm:git:ssh://github.com/Consensys/jc-kzg-4844.git")
                    url.set("https://github.com/Consensys/jc-kzg-4844")
                }
            }
        }
    }
}

tasks.register("checkJarContents") {
    doLast {
        exec {
            executable project.file("src/test/bash/checkResources.sh")
            args "${project.jar.outputs.files.getFiles()[0]}"
        }
    }
}
plugins {
    id "java-library"
    id "me.qoomon.git-versioning" version "6.4.3"
    id "maven-publish"
    id "org.jreleaser" version "1.16.0"
    id "signing"
}

repositories {
    mavenCentral()
}

group = "io.consensys.protocols"

version = "UNKNOWN"

gitVersioning.apply {
    refs {
        tag(".+") {
            version = "\${ref}"
        }
        branch("master") {
            version = "develop"
        }
        branch(".+") {
            version = "\${ref}"
        }
    }
    rev {
        version = "UNKNOWN"
    }
}

java {
    sourceCompatibility = 17
    targetCompatibility = 17
    withJavadocJar()
    withSourcesJar()
}

signing {
    useGpgCmd()
    sign publishing.publications
}

publishing {
    publications {
        mavenJava(MavenPublication) {

            groupId = project.group
            version = project.version

            from components.java

            versionMapping {
                usage("java-api") { fromResolutionOf("runtimeClasspath") }
                usage("java-runtime") { fromResolutionResult() }
            }
            pom {
                name = "${project.name}"
                url = "http://github.com/Consensys/jc-kzg-4844"
                description = "Java wrapper around C-KZG-4844"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = "consensys"
                        name = "Protocols Team"
                        email = "devops@consensys.net"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/Consensys/jc-kzg-4844.git"
                    developerConnection = "scm:git:ssh://github.com/Consensys/jc-kzg-4844.git"
                    url = "https://github.com/Consensys/jc-kzg-4844"
                }
            }
        }
    }
    repositories {
        maven {
            url = layout.buildDirectory.dir("staging-deploy")
        }
    }
}

jreleaser {
    signing {
        active = 'ALWAYS'
        armored = true
    }
    deploy {
        maven {
            nexus2 {
                'maven-central' {
                    active = 'ALWAYS'
                    url = 'https://s01.oss.sonatype.org/service/local'
                    snapshotUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
                    closeRepository = true
                    releaseRepository = true
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }
}

tasks.register("checkJarContents") {
    doLast {
        exec {
            executable project.file("src/test/bash/checkResources.sh")
            args "${project.jar.outputs.files.getFiles()[0]}"
        }
    }
}